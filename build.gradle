plugins {
    id 'java'
}

group = 'p.denis'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven { url "https://repo.clojars.org" }
}

ext {
    graal_version = '22.3.1'
}

sourceSets {
    main {
        resources {
            srcDirs "src/$name/resources", "src/main/clojure"
        }
    }
}

dependencies {
    implementation "org.graalvm.truffle:truffle-api:$graal_version"
    implementation "org.clojure:clojure:1.11.1"
    implementation "instaparse:instaparse:1.4.12"
    implementation "org.clojure:core.match:1.0.1"
    annotationProcessor "org.graalvm.truffle:truffle-dsl-processor:$graal_version"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()

    jvmArgs '-ea',
            '--add-exports',
            'org.graalvm.truffle/com.oracle.truffle.api=ALL-UNNAMED',
            '--add-exports',
            'org.graalvm.truffle/com.oracle.truffle.api.nodes=ALL-UNNAMED',
            '--add-exports',
            'org.graalvm.truffle/com.oracle.truffle.api.staticobject=ALL-UNNAMED'

    testLogging {
        events 'failed'
        exceptionFormat 'full'
    }
}